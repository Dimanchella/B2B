Функция ОтправитьНаСайт(СсылкаНаОбъект, ПолнаяВыгрузка) Экспорт
	Возврат web_Справочники.ОтправитьКонтрагента(СсылкаНаОбъект, ПолнаяВыгрузка);
КонецФункции

Функция ПолучитьСвязанныеСсылки(СсылкаНаОбъект) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Контрагенты.Партнер КАК Партнер
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
	
	СписокСсылок = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		СписокСсылок.Вставить(СсылкаНаОбъект, 0);	
		Возврат СписокСсылок;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	МаксимальныйПриоритет = 0;
	web_ОбщегоНазначения.ЗаписатьЗависимыеСсылкиПоОбъекту(Выборка.Партнер, СписокСсылок, МаксимальныйПриоритет);
	
	Приоритет = МаксимальныйПриоритет + 1;
	
	СписокСсылок.Вставить(СсылкаНаОбъект, Приоритет);
	ВставитьДоговоры(СсылкаНаОбъект, СписокСсылок, Приоритет);
	ВставитьСоглашения(СсылкаНаОбъект, СписокСсылок, Приоритет);
	Возврат СписокСсылок;
КонецФункции


Процедура ВставитьДоговоры(Контрагент, СписокПриемник, Приоритет)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.Партнер КАК Партнер,
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Контрагент = &Контрагент";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;                                        
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МаксимальныйПриоритет = Приоритет;
		web_ОбщегоНазначения.ЗаписатьЗависимыеСсылкиПоОбъекту(Выборка.Организация, СписокПриемник, МаксимальныйПриоритет);
		web_ОбщегоНазначения.ЗаписатьЗависимыеСсылкиПоОбъекту(Выборка.Партнер, СписокПриемник, МаксимальныйПриоритет);
		СписокПриемник.Вставить(Выборка.Ссылка, МаксимальныйПриоритет + 1);	
	КонецЦикла;
КонецПроцедуры

Процедура ВставитьСоглашения(Контрагент, СписокПриемник, Приоритет)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоглашенияСКлиентами.Организация КАК Организация,
	|	СоглашенияСКлиентами.Партнер КАК Партнер,
	|	СоглашенияСКлиентами.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|ГДЕ
	|	СоглашенияСКлиентами.Контрагент = &Контрагент";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;                                        
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МаксимальныйПриоритет = Приоритет;
		web_ОбщегоНазначения.ЗаписатьЗависимыеСсылкиПоОбъекту(Выборка.Организация, СписокПриемник, МаксимальныйПриоритет);
		web_ОбщегоНазначения.ЗаписатьЗависимыеСсылкиПоОбъекту(Выборка.Партнер, СписокПриемник, МаксимальныйПриоритет);
		СписокПриемник.Вставить(Выборка.Ссылка, МаксимальныйПриоритет + 1);	
	КонецЦикла;
КонецПроцедуры
	